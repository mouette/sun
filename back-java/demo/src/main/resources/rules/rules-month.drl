package com.sogeti.sun.demo.business


import java.util.List;

global Parameters parametres;

rule "Init production"
	ruleflow-group "Final"
	when
		$scenario : Scenario( latitude != 0 && longitude != 0)
		not Month()
	then
		List<Double> prods = parametres.getProduction($scenario.getLatitude(), $scenario.getLongitude(), $scenario.getPuissance());
		int i = 0;
		for(Double prod: prods) {
			i++;
			insert(new Month(i, prod));
		}
	end

rule "Init Conso 0"
	ruleflow-group "Final"
	when
		$month: Month( name == 1 && consommation == null)
	then
		modify($month) {
			setConsommation(10d);
		}
	end


rule "Calc Conso"
	ruleflow-group "Final"
	when
		$month: Month(consommation == null)
		$prevMonth: Month( name == $month.getName() - 1, consommation != null)
	then
		modify($month) {
			setConsommation($prevMonth.getConsommation() * 1.01);
		}
	end


rule "Calc Net"
	ruleflow-group "Final"
	when
		$month: Month(net == null && consommation != null && production != null)
	then
		modify($month) {
			setNet($month.getProduction() - $month.getConsommation());
		}
	end